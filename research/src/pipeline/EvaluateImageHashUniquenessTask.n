using Nemerle.Collections;
using System.Drawing;
using System.IO;

[ Record ]
class EvaluateImageHashUniquenessTask : ITask
{
	private parameters : Parameters;

	public Name : string { get { "EvaluateImageHashUniquenessTask" } }

	public MapItems(items : list[ITaskItem]) : list[TaskMapping]
	{
		mutable images     = [];
		mutable thumbnails = [];
		foreach (item is ImageCollectionItem in items)
		{
			def name = Path.GetFileName(item.Path);
			match (Path.GetDirectoryName(item.Path))
			{
			| "images"     => images     ::= (name, item)
			| "thumbnails" => thumbnails ::= (name, item)
			| _            => ()
			}
		}
		mutable mappings = [];
		foreach ((name, image) in images)
		{
			def thumbnail = thumbnails.Find(x => x[0] == name).Value[1];
			def result = ImageHashUniquenessEvaluationItem(Path.Combine("eval",  $"$name hash uniqueness.html"));
			mappings ::= TaskMapping
				( inputs  = [ image, thumbnail ]
				, outputs = [ result ]
				, compute = () => Compute(image, thumbnail, result)
				);
		}
		mappings;
	}

	private Compute
		( thumbnails : ImageCollectionItem
		, images     : ImageCollectionItem
		, evaluation : ImageHashUniquenessEvaluationItem
		) : void
	{
		def map = Hashtable();
		foreach (id in thumbnails.ImageIDs)
		{
			def hash =
				using (bmp = Bitmap(images.MakePath(id)))
					ImageHash.GetHashCode(bmp, parameters);
			map[hash] = id :: map.GetValueOrDefault(hash);
		}
		evaluation.Conflicts = map.MapToArrayFiltered
			( isMatch = pair => pair.Value.Length > 1
			, convert = pair => pair.Value.ToArray()
			);
		evaluation.MakeImagePathFromID = thumbnails.MakePath;
	}
}
