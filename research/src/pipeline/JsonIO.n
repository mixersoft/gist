using Nemerle.Collections;
using Newtonsoft.Json;
using Newtonsoft.Json.Linq;
using System.IO;
using System;

public module JsonIO
{
	//--------------------
	// source JSON data IO
	//--------------------

	public class PhotoInfo
	{
		public ID        : string;
		public RootSrc   : string;
		public ShotID    : string;
		public OwnerID   : string;
		public DateTaken : DateTime;

		public this
			( id        : string
			, rootSrc   : string
			, shotID    : string
			, ownerID   : string
			, dateTaken : DateTime
			)
		{
			ID        = id;
			RootSrc   = rootSrc;
			ShotID    = shotID;
			OwnerID   = ownerID;
			DateTaken = dateTaken;
		}
	}

	public Save(path : string, photos : array[PhotoInfo], baseUrl : string, id : uint) : void
	{
		using (writer = JsonTextWriter(File.CreateText(path)))
		{
			writer.Formatting  = Formatting.Indented;
			writer.Indentation = 1;
			writer.IndentChar  = '\t';

			writer.WriteStartObject();
			{
				writer.WritePropertyName("response");
				writer.WriteStartObject();
				{
					writer.WritePropertyName("castingCall");
					writer.WriteStartObject();
					{
						writer.WritePropertyName("CastingCall");
						writer.WriteStartObject();
						{
							writer.WritePropertyName("ID");
							writer.WriteValue(id);

							writer.WritePropertyName("Auditions");
							writer.WriteStartObject();
							{
								writer.WritePropertyName("Audition");
								writer.WriteStartArray();
								foreach (photo in photos)
								{
									writer.WriteStartObject();

									writer.WritePropertyName("id");
									writer.WriteValue(photo.ID);

									writer.WritePropertyName("Photo");
									writer.WriteStartObject();
									{
										writer.WritePropertyName("Img");
										writer.WriteStartObject();
										{
											writer.WritePropertyName("Src");
											writer.WriteStartObject();
											{
												writer.WritePropertyName("rootSrc");
												writer.WriteValue(photo.RootSrc);
											}
											writer.WriteEnd();
										}
										writer.WriteEnd();

										writer.WritePropertyName("OwnerId");
										writer.WriteValue(photo.OwnerID);

										writer.WritePropertyName("DateTaken");
										writer.WriteValue(photo.DateTaken.ToString("yyyy-MM-dd HH:mm:ss"));
									}
									writer.WriteEnd();

									writer.WritePropertyName("Shot");
									writer.WriteStartObject();
									{
										writer.WritePropertyName("id");
										writer.WriteValue(photo.ShotID);
									}
									writer.WriteEnd();

									writer.WriteEnd();
								}
								writer.WriteEnd();

								writer.WritePropertyName("Baseurl");
								writer.WriteValue(baseUrl);
							}
							writer.WriteEnd();
						}
						writer.WriteEnd();
					}
					writer.WriteEnd();
				}
				writer.WriteEnd();
			}
			writer.WriteEnd();
		}
	}

	public Load(path : string) : array[PhotoInfo] * string * uint
	{
		def data = JObject.Parse(File.ReadAllText(path));

		def castingCall = data["response"]["castingCall"];

		def GetPhotoInfo(record)
		{
			PhotoInfo
				( id        = record["id"] :> string
				, rootSrc   = record["Photo"]["Img"]["Src"]["rootSrc"] :> string
				, shotID    = (record["Shot"]["id"] :> string) ?? ""
				, ownerID   = (record["Photo"]["OwnerId"] :> string) ?? ""
				, dateTaken = DateTime.Parse(record["Photo"]["DateTaken"] :> string)
				)
		}
		( castingCall["CastingCall"]["Auditions"]["Audition"]
			.Children()
			.MapToArray(GetPhotoInfo)
		, castingCall["CastingCall"]["Auditions"]["Baseurl"] :> string
		, castingCall["CastingCall"]["ID"] :> uint
		)
	}

	//---------------
	// event group IO
	//---------------

	[ Record ]
	public class EventInfo
	{
		public FirstPhotoID : string;
		public PhotoCount   : int;
		public BeginDate    : DateTime;
		public EndDate      : DateTime;

		public Children : array[EventInfo];
	}

	public Save(path : string, events : array[EventInfo], noise : array[EventInfo], id : uint) : void
	{
		using (writer = JsonTextWriter(File.CreateText(path)))
		{
			def WriteEvent(e)
			{
				writer.WriteStartObject();

				writer.WritePropertyName("FirstPhotoID");
				writer.WriteValue(e.FirstPhotoID);

				writer.WritePropertyName("PhotoCount");
				writer.WriteValue(e.PhotoCount);

				writer.WritePropertyName("BeginDate");
				writer.WriteValue(e.BeginDate);

				writer.WritePropertyName("EndDate");
				writer.WriteValue(e.EndDate);

				when (e.Children.Length > 0)
				{
					writer.WritePropertyName("Children");
					writer.WriteStartArray();
					foreach (child in e.Children)
						WriteEvent(child);
					writer.WriteEnd();
				}

				writer.WriteEnd();
			}

			writer.Formatting  = Formatting.Indented;
			writer.Indentation = 1;
			writer.IndentChar  = '\t';

			writer.WriteStartObject();
			{
				writer.WritePropertyName("ID");
				writer.WriteValue(id);

				writer.WritePropertyName("Events");
				writer.WriteStartArray();
				foreach (e in events)
					WriteEvent(e);
				writer.WriteEnd();

				writer.WritePropertyName("Noise");
				writer.WriteStartArray();
				foreach (e in noise)
					WriteEvent(e);
				writer.WriteEnd();
			}
			writer.WriteEnd();
		}
	}
}
