using Nemerle.Collections;
using PipelineMacro;
using System.Collections.Generic;
using System.Drawing;
using System.IO;

class EvaluateImageHashUniquenessTask : ITask
{
	public MapItems(items : list[ITaskItem]) : list[TaskMapping]
	{
		def thumbnails = Dictionary();
		foreach (item is ThumbnailCollectionItem in items)
			thumbnails[item.Name] = item;

		// pair up images and thumbnails
		mutable mappings = [];
		foreach (item is ImageCollectionItem when thumbnails.ContainsKey(item.Name) in items)
		{
			mappings ::= TaskMapping
				( this
				, [ item, thumbnails[item.Name] ]
				, [ ImageHashUniquenessEvaluationItem(item.Name) ]
				);
		}
		mappings;
	}

	private Compute
		( [InItem]  images     : ImageCollectionItem
		, [InItem]  thumbnails : ThumbnailCollectionItem
		, [OutItem] evaluation : ImageHashUniquenessEvaluationItem
		, hashMethod      : ImageHash.HashMethod
		, hashedImageSize : int
		, hashedImageBpp  : int
		) : void
	{
		def map = Hashtable();
		foreach (id in thumbnails.ImageIDs)
		{
			def hash =
				using (bmp = Bitmap(images.MakePath(id)))
					ImageHash.GetHashCode(bmp, hashMethod, hashedImageSize, hashedImageBpp);
			map[hash] = id :: map.GetValueOrDefault(hash);
		}
		evaluation.Conflicts = map.MapToArrayFiltered
			( isMatch = pair => pair.Value.Length > 1
			, convert = pair => pair.Value.ToArray()
			);
		evaluation.MakeImagePathFromID = thumbnails.MakePath;
	}
}
