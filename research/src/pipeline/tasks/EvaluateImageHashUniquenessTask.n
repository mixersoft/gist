using Nemerle.Collections;
using PipelineMacro;
using System.Drawing;
using System.IO;

class EvaluateImageHashUniquenessTask : ITask
{
	public MapItems(items : list[ITaskItem]) : list[TaskMapping]
	{
		mutable mappings = [];
		foreach ((name, images, thumbnails) in items.Join.[ImageCollectionItem, ThumbnailCollectionItem]())
		{
			mappings ::= TaskMapping
				( this
				, [ images, thumbnails ]
				, [ ImageHashUniquenessEvaluationItem(name) ]
				);
		}
		mappings;
	}

	private Compute
		( [InItem]  images     : ImageCollectionItem
		, [InItem]  thumbnails : ThumbnailCollectionItem
		, [OutItem] evaluation : ImageHashUniquenessEvaluationItem
		, hashMethod      : ImageHash.HashMethod
		, hashedImageSize : int
		, hashedImageBpp  : int
		) : void
	{
		def map = Hashtable();
		foreach (id in thumbnails.ImageIDs)
		{
			def hash =
				using (bmp = Bitmap(images.MakePath(id)))
					ImageHash.GetHashCode(bmp, hashMethod, hashedImageSize, hashedImageBpp);
			map[hash] = id :: map.GetValueOrDefault(hash);
		}
		evaluation.Conflicts = map.MapToArrayFiltered
			( isMatch = pair => pair.Value.Length > 1
			, convert = pair => pair.Value.ToArray()
			);
		evaluation.MakeImagePathFromID = thumbnails.MakePath;
	}
}
