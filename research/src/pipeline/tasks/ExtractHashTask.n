using Nemerle.Collections;
using PipelineMacro;
using System.Drawing;
using System;

class ExtractHashTask : ITask
{
	public MapItems(items : list[ITaskItem]) : list[TaskMapping]
	{
		mutable mappings = [];
		foreach (item is SampleImageCollectionItem in items)
			mappings ::= TaskMapping(this, [ item ], [ ImageHashItem(item.Name) ]);
		mappings;
	}

	private Compute
		( [InItem]  imageSample : SampleImageCollectionItem
		, [OutItem] hashes      : ImageHashItem
		, hashMethod      : ImageHash.HashMethod
		, hashedImageSize : int
		, hashedImageBpp  : int
		) : void
	{
		def Hash(id, map)
		{
			using (bmp = Bitmap(imageSample.MakePath(id)))
				map.Add(id, ImageHash.GetHashCode(bmp, hashMethod, hashedImageSize, hashedImageBpp));
		}
		hashes.ImageHashes = imageSample.ImageIDs.Fold(Map(), Hash);
	}
}
