using Nemerle.Collections;
using Nemerle.Utility;
using System.IO;
using System.Net;
using System;

[ Record ]
class DownloadDataTask : ITask
{
	private parameters : Parameters;

	public Name : string { get { "DownloadDataTask" } }

	public MapItems(items : list[ITaskItem]) : list[TaskMapping]
	{
		mutable mappings = [];
		foreach (item is JsonItem when item.Path.StartsWith(@"data\") in items)
		{
			def result = ImageCollectionItem
				(Path.Combine("images", Path.GetFileNameWithoutExtension(item.Path)));
			mappings ::= TaskMapping
				( inputs  = [ item ]
				, outputs = [ result ]
				, compute = () => Compute(item, result)
				);
		}
		mappings;
	}

	private Compute(jsonItem : JsonItem, images : ImageCollectionItem) : void
	{
		def downloadPrefix = "/" + parameters.DownloadImagePrefix;

		def client = WebClient();

		images.ImageIDs = array(jsonItem.Photos.Length);

		_ = Directory.CreateDirectory(Path.GetDirectoryName(images.Path));
		_ = Directory.CreateDirectory(images.Path);

		foreach (photo in jsonItem.Photos with i)
		{
			_ = Directory.CreateDirectory(Path.GetDirectoryName(images.Path));
			client.DownloadFile
				( address  = jsonItem.BaseUrl + photo.RootSrc.Replace("/", downloadPrefix)
				, fileName = images.MakePath(photo.ID)
				);
			images.ImageIDs[i] = photo.ID;
		}
	}
}
