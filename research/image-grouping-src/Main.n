using Emgu.CV.CvEnum;
using Emgu.CV.Structure;
using Emgu.CV;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Newtonsoft.Json.Linq;
using Profiling;
using System.IO;
using System.Math;
using System.Net;
using System;

using SCG = System.Collections.Generic;

[assembly: ProfSetup(Disabled) ]

variant RunMode
{
| Normal
| OptimizeThreshold { min : double; max : double; count : int }
| OptimizeImageSize
}

module Main
{
	[ ProfDump ]
	private PrintProfileInfo() : void
	{
	}

	private Run() : void
	{
		def parameters = Parameters();
		TaskManager
			( tasks =
				[ LoadJsonDataTask          ():ITask
				, DownloadDataTask          (parameters)
				, ExtractDescriptorsTask    (parameters)
				, ClusterDataTask           (parameters)
				, LoadReferenceClustersTask ()
				, EvaluateClustersTask      ()
				]
			, persistData     = true
			, displayProgress = true
			).Compute();
		PrintProfileInfo();
	}

	private RunThresholdOptimization(min : double, max : double, count : int) : void
	{
		def parameters = MockParameters
			( downloadImagePrefix = ""
			, imageSize           = (0, 0)
			, clusteringThreshold = 0.2
			);

		def taskManager = TaskManager
			( tasks =
				[ LoadJsonDataTask          ():ITask
				, DownloadDataTask          (parameters)
				, ExtractDescriptorsTask    (parameters)
				, ClusterDataTask           (parameters)
				, LoadReferenceClustersTask ()
				, EvaluateClustersTask      ()
				]
			, persistData     = false
			, displayProgress = true
			);

		def taskPath = @"eval\peter-alice.txt";
		def task =
			match (taskManager.Items.Find(item => item.Path == taskPath))
			{
			| Some(item is ClusterEvaluationItem) => item
			| _                                   => throw Exception($"'$taskPath' not found.")
			};

		def Run(threshold)
		{
			parameters.ClusteringThreshold = threshold;

			taskManager.DiscardItemData(@"clusters\peter-alice.txt");
			taskManager.Compute();

			Console.WriteLine($"$threshold\t$(task.Value)");
		}

		Console.WriteLine("Threshold optimization:");
		def Lerp(step : int)
		{
			min + (max - min) * step / count
		}
		$[0 .. count].Map(Lerp).Iter(Run);
	}

	private RunImageSizeOptimization() : void
	{
		def parameters = MockParameters
			( downloadImagePrefix = ""
			, imageSize           = (0, 0)
			, clusteringThreshold = 0.0
			);

		def taskManager = TaskManager
			( tasks =
				[ LoadJsonDataTask          ():ITask
				, DownloadDataTask          (parameters)
				, ExtractDescriptorsTask    (parameters)
				, ClusterDataTask           (parameters)
				, LoadReferenceClustersTask ()
				, EvaluateClustersTask      ()
				]
			, persistData     = false
			, displayProgress = false
			);

		def dataSet = "peter-alice";

		def taskPath = $@"eval\$dataSet.txt";
		def task =
			match (taskManager.Items.Find(item => item.Path == taskPath))
			{
			| Some(item is ClusterEvaluationItem) => item
			| _                                   => throw Exception($"'$taskPath' not found.")
			};

		def (min, max, count) = (0.1, 0.9, 80);

		def Run(w, h)
		{
			def Run(threshold)
			{
				parameters.ClusteringThreshold = threshold;

				taskManager.DiscardItemData($@"clusters\$dataSet.txt");
				taskManager.Compute();

				Console.WriteLine($"$(w)x$(h)\t$threshold\t$(task.Value)");
			}

			parameters.ImageSize = (w, h);

			taskManager.DiscardItemData($@"descriptors\$dataSet");

			def Lerp(step : int)
			{
				min + (max - min) * step / count
			}
			$[0 .. count].Map(Lerp).Iter(Run);
		}

		Console.WriteLine("Image size optimization:");
		[ (90, 90), (120, 120), (256, 256) ].Iter(Run);
	}

	public Main() : void
	{
		mutable runMode     = RunMode.Normal();
		mutable displayHelp = false;
		def opts =
			[ Getopt.CliOption.String
				( "-optimize-threshold"
				, "Optimize the threshold meta-parameter over the given range: 'min max count'."
				, value =>
					regexp match (value)
					{
					| @"(?<min:double>[^ ]+) (?<max:double>[^ ]+) (?<count:int>[^ ]+)" =>
						runMode = RunMode.OptimizeThreshold(min, max, count);
					| _ =>
						Console.WriteLine($"Invalid threshold range: '$value'.");
						displayHelp = true;
					}
				)
			, Getopt.CliOption.Flag
				( "-optimize-image-size"
				, "Optimize the image size."
				, () => runMode = RunMode.OptimizeImageSize()
				)
			, Getopt.CliOption.Flag
				( "-help"
				, [ "-?" ]
				, "Display this message."
				, () => displayHelp = true
				)
			];
		Getopt.Parse(opts);

		if (displayHelp)
		{
			Console.WriteLine(Getopt.Usage(opts));
		}
		else
		{
			match (runMode)
			{
			| Normal                             => Run()
			| OptimizeThreshold(min, max, count) => RunThresholdOptimization(min, max, count)
			| OptimizeImageSize                  => RunImageSizeOptimization()
			}
		}
	}
}
