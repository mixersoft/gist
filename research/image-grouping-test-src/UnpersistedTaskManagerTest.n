using NUnit.Framework;
using Nemerle.Collections;
using System.Linq;
using System;

[ TestFixture ]
public class UnpersistedTaskManagerTest
{
	private mutable taskManager : TaskManager;
	private mutable items       : Hashtable[string, ITaskItem];

	[ SetUp ]
	public Setup() : void
	{
		def AddTaskItem(name, hasCache)
		{
			items.Add
				( name
				, MockTaskItem
					( path         = name
					, hasCache     = hasCache
					, isSaved      = false
					, isLoaded     = false
					, computeCount = 0
					)
				);
		}
		items = Hashtable();
		[ ("item-1", false)
		, ("item-2", true)
		, ("item-3", false)
		, ("item-4", true)
		, ("item-5", false)
		].Iter(AddTaskItem);

		taskManager = TaskManager
			( tasks =
				[ MockTask
					( name    = "task-1"
					, itemMap =
						[ ([], ["item-1"])
						, ([], ["item-2"])
						]
					, items = items
					)
				, MockTask
					( name    = "task-2"
					, itemMap =
						[ (["item-1", "item-2"], ["item-3"])
						, (["item-1", "item-2"], ["item-4"])
						]
					, items = items
					)
				, MockTask
					( name    = "task-3"
					, itemMap =
						[ (["item-1", "item-3"], ["item-5"]) ]
					, items = items
					)
				]
			, persistData     = false
			, displayProgress = false
			);
	}

	[ Test ]
	public TestConstruction() : void
	{
		def IsLoaded(name)
		{
			(items[name] :> MockTaskItem).IsLoaded
		}

		def items = taskManager.Items.ToArray();
		Array.Sort(items);

		CollectionAssert.AreEquivalent
			( expected = $[1..5].Map(n => $"item-$n")
			, actual   = items
			, message  = "Were all items discovered?"
			);

		Assert.IsFalse
			( condition = items.Exists(IsLoaded)
			, message   = "Were any items loaded on startup?"
			);
	}

	[ Test ]
	public TestDiscardItemData() : void
	{
		def Discard = taskManager.DiscardItemData;

		foreach (n in [1 .. 4])
			_ = Assert.DoesNotThrow(() => Discard($"item-$n"));

		_ = Assert.Catch.[ArgumentException](() => Discard(""));
		_ = Assert.Catch.[ArgumentException](() => Discard("item-0"));
	}

	[ Test ]
	public TestCompute() : void
	{
		def IsLoaded(name)
		{
			(items[name] :> MockTaskItem).IsLoaded
		}
		def IsSaved(name)
		{
			(items[name] :> MockTaskItem).IsSaved
		}

		taskManager.Compute();

		def items = taskManager.Items.ToArray();
		Array.Sort(items);

		CollectionAssert.AreEqual
			( expected = [ 1, 0, 1, 1, 1 ]
			, actual   = items.Map(GetComputeCount)
			, message  = "Were the appropriate items computed?"
			);

		CollectionAssert.AreEqual
			( expected = [ false, true, false, false, false ]
			, actual   = items.Map(IsLoaded)
			, message  = "Were the appropriate items loaded?"
			);

		Assert.IsFalse
			( condition = items.Exists(IsSaved)
			, message   = "Has any item been saved?"
			);
	}

	[ Test ]
	public TestRecompute() : void
	{
		def ResetItemData()
		{
			items.Values.Iter(item => (item :> MockTaskItem).Reset());
		}

		def values =
			[ ("item-1", [ 1, 0, 1, 1, 1 ])
			, ("item-2", [ 0, 1, 1, 1, 1 ])
			, ("item-3", [ 0, 0, 1, 0, 1 ])
			, ("item-4", [ 0, 0, 0, 1, 0 ])
			, ("item-5", [ 0, 0, 0, 0, 1 ])
			];

		foreach ((discarded, expected) in values)
		{
			taskManager.Compute();

			ResetItemData();
			taskManager.DiscardItemData(discarded);

			taskManager.Compute();

			foreach (count in expected with i)
			{
				def name = $"item-$(i+1)";
				def verb = if (count == 0) "skipped" else "computed";
				Assert.AreEqual
					( actual   = GetComputeCount(name)
					, expected = count
					, message  = $"Was '$name' $verb after discarding '$discarded'?"
					);
			}
		}
	}

	private GetComputeCount(itemName : string) : int
	{
		match (items.Get(itemName))
		{
		| Some(item) => (item :> MockTaskItem).ComputeCount
		| None       => 0
		}
	}
}
