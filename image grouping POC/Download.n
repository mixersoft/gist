// REFERENCE: Newtonsoft.Json.dll

using Nemerle.Collections;
using Newtonsoft.Json.Linq;
using System.Console;
using System.IO.Directory;
using System.IO.Path;
using System.IO;
using System.Net;
using System;

def client = WebClient();

def ProcessFile(file, dstDir)
{
	def data = JObject.Parse(File.ReadAllText(file));

	def castingCall = data["response"]["castingCall"];
	def baseUrl     = "http://dev.snaphappi.com" + castingCall["CastingCall"]["Auditions"]["Baseurl"];
	def photos      = castingCall["CastingCall"]["Auditions"]["Audition"];

	def count = photos.Children().NCount();

	foreach (photo in photos.Children() with i)
	{
		def id      = photo["id"];
		def rootSrc = photo["Photo"]["Img"]["Src"]["rootSrc"];
		def image   = baseUrl + rootSrc.ToString().Replace("/", "/tn~");

		WriteLine($"\t$i/$count $id");

		client.DownloadFile(image, Combine(dstDir, id + ".jpg"));
	}
}

foreach (file in GetFiles("data"))
{
	def dstDir = Combine("images", GetFileNameWithoutExtension(file));
	unless (Directory.Exists(dstDir))
	{
		WriteLine(dstDir);

		_ = CreateDirectory(dstDir);

		ProcessFile(file, dstDir);
	}
}
